plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'tech.favware'
version = '0.0.5'
description = 'A Java8 implementation of a Result monad inspired by Rust\'s Result struct'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    mavenLocal()
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'java8-result'
            from components.java

            pom {
                name = 'Java8 Result'
                description = 'A Java8 implementation of a Result monad inspired by Rust\'s Result struct.'
                url = 'https://github.com/favware/java8-result/'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'favna'
                        name = 'Jeroen Claassens'
                        email = 'support@favware.tech'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/favware/java8-result.git'
                    developerConnection = 'scm:git:ssh://github.com:favware/java8-result.git'
                    url = 'http://github.com/favware/java8-result/tree/main'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSHR"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty("osshr.user") ?: System.getenv("OSSHR_USERNAME")
                password = project.findProperty("osshr.key") ?: System.getenv("OSSHR_TOKEN")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/favware/java8-result")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
            }
        }
        maven {
            name = "Local"
            url = layout.buildDirectory.dir('repo')
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}